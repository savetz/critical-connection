

NEWKEYS	MACRO

BOOTJMP	DW	0
RCONSTAT DW	0	;; Address CPM thinks has address of
INITKEY	LHLD	1	;; console status routine
	SHLD	BOOTJMP
	LXI	D,4
	DAD	D
	SHLD	RCONSTAT
	MOV	A,M
	STA	CONST+1	;; LOW
	INX	H
	MOV	A,M
	STA	CONST+2	;; HIGH
	LHLD	RCONSTAT
	SHLD	MREALD+1
	SHLD	MFAKED+1
	LHLD	1
	LXI	D,6
	DAD	D
	SHLD	CONIN+1
	CALL	MAKEFAKE
	RET

MAKEREAL LHLD	CONST+1
MREALD	SHLD	00
	RET
MAKEFAKE LXI	H,FAKESTAT
MFAKED	SHLD	00
	RET
FAKESTAT XRA	A
	RET
CONST	JMP     0000
CONIN	JMP	0000

INKEY	CALL	DIRECTIN
	ORA	A
	RZ		;; Short if no key, but there has to be
;;	JZ	GETHI
	PUSH	PSW
	LDA	NUMKEYS
	MOV	E,A
	MVI	D,0
	LXI	H,KEYBUF
	DAD	D
	POP	PSW
	MOV	M,A
	CPI	'@'
	CZ	ISATSIGN
	LDA	NUMKEYS
	INR	A
	STA	NUMKEYS
	LDA	WANTCH
	CPI	'Y'
	JZ	GOTNWANT
	RET		;; Will go to GETHI or IMLOST
;;	JMP	GETHI

ISATSIGN LDA	LASTCHAR ;; CR @  is escape sequence
	CPI	CR
	JZ	MENU
	DCX	H
	MOV	A,M
	CPI	CR
	JZ	MENU
	RET
	

ONKEYBD LDA	ISKEYON
	CPI	'Y'
	JZ	KEYWASON

	LXI	H,INKEY
	SHLD	BEENHIT+1
	LXI	H,KBOOT
	SHLD	SBOOT+1
	MVI	A,'Y'
	STA	ISKEYON
	JMP	ACTIVATE

KEYWASON LXI	H,HITME
	SHLD	BEENHIT+1
	LXI	H,BOOT
	SHLD	SBOOT+1
	MVI	A,'N'
	STA	ISKEYON
	JMP	ACTIVATE

ACTIVATE CALL	IPRINT
 DB CR,LF,'For the change to work the ATARI must be rebooted'
 DB CR,LF,'by turning it off then on.',CR,LF,0
	JMP	ISERVATR

ISKEYON DB 'N'	;; No

KEYBOARD	CALL	GETA
	CALL	ADDCHKSM
	CALL	EAT3
	CALL	ACK
;LDA	DAUX1
;MOV	C,A
;LDA	LASTKX1
;CMP	C
;JZ	RETRYING
;LDA	DAUX1
;STA	LASTKX1
	MVI	A,'Y'
	STA	WANTCH
	LDA	NUMKEYS
	CPI	0
	JNZ	GOTNWANT
	JMP	GETHI

;;RETRYING	MVI	A,'R'
;;	CALL	PUTCHAR
;;	JMP	RETRANCH

GOTNWANT MVI	A,'N'
	STA	WANTCH
	LDA	NUMKEYS
	DCR	A
	STA	NUMKEYS
	LDA	KEYBUF
	STA	LASTCHAR
	LXI	H,KEYBUF+1
	LXI	D,KEYBUF
	LXI	B,255
	CALL	BMOVER
RETRANCH MVI	C,'C'
	CALL	PUTC	;;	COMPLETE
	LDA	LASTCHAR
	CPI	13H
	JZ	CLEFT
	CPI	8	;;	ASCII	BS
	JZ	CLEFT
	CPI	4
	JZ	CRIGHT
	CPI	18H
	JZ	CDOWN
	CPI	5
	JZ	CUP
	CPI	CR
	JZ	CRLF9B
	CPI	7FH
	JZ	DEL
	CALL	PUTCHAR
	LDA	LASTCHAR
ALLFIX	MOV	C,A
	CALL	PUTC	;;	DATA
	CALL	PUTC	;;	CHECKSUM
	JMP	GETHI

CLEFT	MVI	A,8
	CALL	PUTCHAR
	MVI	A,1EH
	JMP	ALLFIX
CRIGHT	MVI	A,' '
	CALL	PUTCHAR
	MVI	A,1FH
	JMP	ALLFIX
CDOWN	MVI	A,LF
	CALL	PUTCHAR
	MVI	A,1DH
	JMP	ALLFIX
CUP	MVI	A,1AH
	DB	0,0,0
;;	CALL	PUTCHAR
	MVI	A,1CH
	JMP	ALLFIX
CRLF9B	MVI	A,CR
	CALL	PUTCHAR
	MVI	A,LF
	CALL	PUTCHAR
	MVI	A,9BH
	JMP	ALLFIX
DEL	MVI	A,8
	CALL	PUTCHAR
	MVI	A,' '
	CALL	PUTCHAR
	MVI	A,8
	CALL	PUTCHAR
	MVI	A,7EH
	JMP	ALLFIX

DIRECTIN PUSH	H ! PUSH D ! PUSH B
	CALL	CONST
	CPI	0
	JZ	DIRDN
	CALL	CONIN
DIRDN	POP	B ! POP	D ! POP	H
	RET	

LASTKX1	DB	1
NUMKEYS	DB	0
LASTCHAR	DB	' '
WANTCH		DB	'Y'

KEYBUF	DB	' ',7EH
	DS	258

	ENDM



