DISKPRIN: MVI	A,'Y'
	STA	ISRECON		
	CALL	IPRINT
 DB CR,LF,'What file would you like the ATARI printer ouput to be put in?',0
	CALL	GETNAME
	CALL	INITFILE
	CALL	DELETE
	CALL	MAKEFILE
	CALL	OPEN
	MVI	A,0C9H	;; RET
	STA	NONEWB
	LXI	H,RALLFULL
	SHLD	JTOFULL+1
	LXI	H,RDISK
	SHLD	GODISK+1
	MVI	A,0C3H	;; JMP
	STA	RECNO80
	JMP	ISERVATR

ISRECON: DB 'N'

RDISK:	CALL	GETA
	CALL	ADDCHKSM
	IF HOME
	STA	DISPLAY+2
	ENDIF
	CPI	'S'
	JZ	RDSTAT
	CPI	'W'
	JZ	RDWRITE
	CPI	'R'
	JZ	RDREAD
	CPI	'P'
	JZ	RDWRITE
	CPI	'!'
	JZ	RFORMAT
	LDA	NUMLOST
	ADI	2
	STA	NUMLOST
	IF DEBUG2
	CALL	PUTHEX
	CALL	PUTCHAR
	ENDIF
	JMP	GETHI

RFORMAT: JMP	GULP3
RDSTAT:	JMP	GULP3
RDREAD:	JMP	GULP3
GULP3:	CALL	GETA
	IF HOME
	STA	DISPLAY+4
	CALL	GETA
	STA	DISPLAY+6
	CALL	GETA
	STA	DISPLAY+8
	ENDIF
	IF NOT HOME
	CALL	GETA
	CALL	GETA
	ENDIF
	JMP	GETHI

RDWRITE: CALL	GETA
	CALL	ADDCHKSM
	CALL	GETA
	CALL	ADDCHKSM
	MOV	C,A
	CALL	GETA
	CMP	C
	JNZ	GETHI
	MVI	C,129
GULP129: CALL	GETA
	DCR	C
	JNZ	GULP129
	IF HOME
	MVI	A,'W'
	CALL	PUTCHAR
	ENDIF
	JMP	GETHI


RALLFULL: LXI	D,DATA	;; Save bottom 128 bytes
	CALL	INTOBUF
	CALL	WRITEB

	LHLD	NUMLEFT
	MOV	B,H
	MOV	C,L
	LXI	H,DATA
	LXI	D,128
	DAD	D
	LXI	D,DATA
	CALL	BMOVER	;; Move all data down 128

	LXI	H,DATA
	SHLD	NEXTCHAR
	LHLD	NUMLEFT
	LXI	D,DATA
	DAD	D
	SHLD	ENDBUF	;; adjust pointers
	JMP	ROOMLEFT


NORMPRIN: LDA	ISRECON
	CPI	'N'	;; Yes or No
	JZ	GETHI	;; if N then already normal
	MVI	A,'N'	;; Normal
	STA	ISRECON
	LHLD	ENDBUF
	CALL	ADDCRLF	;; ADD a CR and a LF
	CALL	APNDEOF	;; Append a EOF	
	CALL	SAVEIT
	CALL	CLOSE
	MVI	A,0	;; NOP
	STA	NONEWB	;; Was a return there
	LXI	H,ALLFULL
	SHLD	JTOFULL+1
	LXI	H,DISK
	SHLD	GODISK+1
	MVI	A,0C2H	;; JNZ
	STA	RECNO80	;; now we split lines again
	LXI	H,0
	SHLD	NUMLEFT
	LXI	H,DATA
	SHLD	NEXTCHAR
	SHLD	ENDBUF
	JMP	ISERVATR

APNDEOF: MVI	A,128	;; Put 128 bytes of 1A at EOF
EOFL:	MVI	M,1AH	;; So that all of the data will get
	INX	H		;	stored
	DCR	A
	JNZ	EOFL
	RET


SAVEIT:	LXI	D,DATA	;; DE=beginning
			;; HL=end	*******
NEXT128: CALL	INTOBUF	;; DE = beginning of next 128 to save
	CALL	WRITEB
	LXI	B,128
	XCHG			;; DE - HL SWOP
	DAD	B
	XCHG			;; DE=DE+128
	CALL	CMPDEHL	;; Are we at the end yet?
	JM	NEXT128	;; No.
	JZ	NEXT128	;; Real close!
	RET

CMPDEHL: MOV	A,D
	CMP	H
	RNZ	
	MOV	A,E
	CMP	L
	RET
			;;


